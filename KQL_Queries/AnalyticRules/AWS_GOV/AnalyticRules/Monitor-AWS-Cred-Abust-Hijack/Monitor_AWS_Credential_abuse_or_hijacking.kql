// Description:
// Looking for GetCallerIdentity Events where the UserID Type is AssumedRole 
// An attacker who has assumed the role of a legitimate account can call the GetCallerIdentity function to determine what account they are using.
// A legitimate user using legitimate credentials would not need to call GetCallerIdentity since they should already know what account they are using.
// More Information: https://duo.com/decipher/trailblazer-hunts-compromised-credentials-in-aws
// AWS STS GetCallerIdentity API: https://docs.aws.amazon.com/STS/latest/APIReference/API_GetCallerIdentity.html

let timeframe = 1d;
AWSCloudTrailLogs
| where TimeGenerated > ago(timeframe)
| where EventName =~ "GetCallerIdentity" and UserIdentityType =~ "AssumedRole" 
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated) by SourceIpAddress, EventName, EventTypeName, UserIdentityType, UserIdentityAccountId, UserIdentityPrincipalid, 
UserAgent, UserIdentityUserName, SessionMfaAuthenticated,AWSRegion, EventSource, AdditionalEventData, ResponseElements
| extend timestamp = StartTimeUtc, AccountCustomEntity = UserIdentityUserName, IPCustomEntity = SourceIpAddress
| sort by EndTimeUtc desc nulls last