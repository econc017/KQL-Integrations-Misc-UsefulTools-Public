// Description:
// Multi-Factor Authentication (MFA) helps you to prevent credential compromise. This alert identifies logins to the AWS Management Console without MFA.
// You can limit this detection to trigger for adminsitrative accounts if you do not have MFA enabled on all accounts.
// This is done by looking at the eventName ConsoleLogin and if the AdditionalEventData field indicates MFA was NOT used 
// and the ResponseElements field indicates NOT a Failure. Thereby indicating that a non-MFA login was successful.

let timeframe = 1d;
AWSCloudTrailLogs
| where TimeGenerated > ago(timeframe)
| where EventName =~ "ConsoleLogin" 
| extend MFAUsed = tostring(parse_json(AdditionalEventData).MFAUsed), LoginResult = tostring(parse_json(ResponseElements).ConsoleLogin)
| where MFAUsed !~ "Yes" and LoginResult !~ "Failure"
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated) by EventName, EventTypeName, LoginResult, MFAUsed, UserIdentityAccountId,  UserIdentityPrincipalid, UserAgent, 
UserIdentityUserName, SessionMfaAuthenticated, SourceIpAddress, AWSRegion
| extend timestamp = StartTimeUtc, AccountCustomEntity = UserIdentityUserName, IPCustomEntity = SourceIpAddress